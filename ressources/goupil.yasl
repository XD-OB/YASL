#!yasl

// Outil de statistique comptable
// Propriete exclusive de la societe Goupil sa
//


// 7, 8 et 9 pour le temp des fonctions
// 1 bloque pour les data


// liste sur 0
{
	0.0 :7
	0.0 :8
	#
	#(
		][
		3 #!
		2 ^
		"A" == ?
			( =:7 0.0 :7 7:+:7 7:+:7 ) :
			( =:8 0.0 :8 8:+:8 8:+:8 )
		4 #!

	)
	8:=:8 7:=:8 8:+:8 8:"":8 8:+
	" (" + 7:"":7 7:+ + "/" + 8:"":8 8:+ + ")" +
} balance



{
	1:#:1 1:#=
	balance
	"Balance generale : " 2 ^ + print "\n" print
} balance_generale



// tri unique n'utilise que la pile courante qui doit etre la meme en entree et en sortie
{
	# 1 -
	#(
	   # 1 -
	   #(
		2 #=
		== ? ( ! "" )
		2 #=
		> ? ( 2 ^ )
		# !^
	    )
	   # !^
	)
	1 @ ( = "" == %? (2 ^ !) )
} tri_unique



// liste sur 0
{
	#
	#(
		][
		5 #!
		=:7
		3 #!
	)
	7:tri_unique:7
	7:#:7 7:#=
	7:#:7 7:#!
} extrait_client_fournisseur




{
	1:#:1 1:#=
	extrait_client_fournisseur
	# #=:3
	# #!

	3:#:3
	3:#(	0:(
		1:#:1 1:#=
		#
		#(
			= ][ 5 #!
			3:= ==:9
			2 #!
			9:? ( 0:=:4 )
			!
		 )
		4:#:4
		4:#=
		balance
		"  " print 3:print " : " print print "\n" print
		4:#:4 4:#!
		):0
		
	   )
} balance_client_fournisseur






// liste sur 0
{
	#
	#(
		][
		2 #!
		=:7
		6 #!
	)
	7:tri_unique:7
	7:#:7 7:#=
	7:#:7 7:#!
} extrait_credit




{
	1:#:1 1:#=
	extrait_credit
	# #=:3
	# #!

	3:#:3
	3:#(	0:(
		1:#:1 1:#=
		#
		#(
			= ][ 2 #!
			3:= ==:9
			5 #!
			9:? ( 0:=:4 )
			!
		 )
		4:#:4
		4:#=
		balance
		"  " print 3:print " : " print print "\n" print
		4:#:4 4:#!
		):0
		
	   )
} balance_credit






{
	"":9
	][
	#(
		= ":" !=:8
		= "\n" !=:8
		= "\r" !=:8
		2:8 8:#&&:8
		8:? 0:( =:9 2:9 9:^:9 9:+:9 ) : "":9
		!
		
	)
	9:#
	=:8
	#( 9:=:9 "":9 9:!=:9 9:? 9:= : (1:8 8:-:8) 9:! )
	8:=
	8:!
} decoupe_ligne




// format du ficher de l'erp
// date:numero_de_facture_ou_commande:client_fournisseur:achat_vente:montant:budget:employe
// stocke sur la pile 1

{
	read
	@ (
		decoupe_ligne
		[] =:1 !
		read
	)
	!
} charge_info


// debut du programme

2 #!

charge_info
//print_stack
balance_generale

"Balance Clients/Fournisseurs\n" print
balance_client_fournisseur
"Balance par ligne de credit\n" print
balance_credit

